interface = { SOI ~ interface_header ~ "{" ~ interface_body ~ "}" ~ EOI }

interface_header = { "[" ~ interface_attributes ~ "]" ~ "interface" ~ Identifier }

interface_attributes = { interface_attribute ~ ("," ~ interface_attribute)* }
interface_attribute  = {
    uuid_attribute
  | version_attribute
  | pointer_default_attribute
  | ms_union_attribute
}

uuid_attribute = {"uuid" ~ "(" ~ Uuid_rep ~ ")"}
version_attribute = { "version" ~ "(" ~ Integer_literal ~ ("." ~ Integer_literal)? ~ ")" }
pointer_default_attribute = { "pointer_default" ~ "(" ~ ptr_attr ~ ")" }
ms_union_attribute = { "ms_union" } // MS-RPCE 2.2.4.5

ptr_attr   = {
    "ref"
  | "unique"
  | "ptr"
}

interface_body = { import* ~ interface_component+ }

// Imports
import      = { "import" ~ import_list ~ ";" }
import_list = { import_name ~ ("," ~ import_name)* }
import_name = { "\"" ~ Import_string ~ "\"" }

// Interface components
interface_component = { export | op_declarator ~ ";" }
export              = { type_declarator ~ ";" | const_declarator ~ ";" }

// Constants
const_declarator   = { "const" ~ const_type_spec ~ Identifier ~ "=" ~ const_exp }
const_type_spec    = { primitive_integer_type | "char" | "void" ~ "*" | "char" ~ "*" }
const_exp          = { integer_const_exp | Identifier | string | character_constant | "NULL" | "TRUE" | "FALSE" }
integer_const_exp  = { conditional_exp }
conditional_exp    = { logical_or_exp ~ ("?" ~ integer_const_exp ~ ":" ~ conditional_exp)? }
logical_or_exp     = { logical_and_exp ~ ("||" ~ logical_and_exp)* }
logical_and_exp    = { inclusive_or_exp ~ ("&&" ~ inclusive_or_exp)* }
inclusive_or_exp   = { exclusive_or_exp ~ ("|" ~ exclusive_or_exp)* }
exclusive_or_exp   = { and_exp ~ ("^" ~ and_exp)* }
and_exp            = { equality_exp ~ ("&" ~ equality_exp)* }
equality_exp       = { relational_exp ~ (("==" | "!=") ~ relational_exp)* }
relational_exp     = { shift_exp ~ (("<" | ">" | "<=" | ">=") ~ shift_exp)* }
shift_exp          = { additive_exp ~ (("<<" | ">>") ~ additive_exp)* }
additive_exp       = { multiplicative_exp ~ (("+" | "-") ~ multiplicative_exp)* }
multiplicative_exp = { unary_exp ~ (("*" | "/" | "%") ~ unary_exp)* }
unary_exp          = { primary_exp ~ (("+" | "-" | "~" | "!") ~ primary_exp)* }
primary_exp        = { Integer_literal | Identifier | "(" ~ const_exp ~ ")" }

type_declarator     = { "typedef" ~ type_attribute_list? ~ type_spec ~ declarators }
type_attribute_list = { "[" ~ type_attribute ~ ("," ~ type_attribute)* ~ "]" }

// Type specifiers
simple_type_spec       = { base_type_spec | Identifier }
type_spec              = { constructed_type_spec | simple_type_spec }
constructed_type_spec  = { struct_type | union_type | enumeration_type | pipe_type }
declarators            = { declarator ~ ("," ~ declarator)* }
direct_declarator_base = { Identifier | "(" ~ declarator ~ ")" }
direct_declarator      = { direct_declarator_base ~ (array_bounds_declarator | function_declarator)* }

// Struct declarator
struct_type            = { "struct" ~ tag? ~ "{" ~ member_list ~ "}" }
tag                      = { Identifier }
member_list              = { member ~ (member)* }
member                   = { field_declarator ~ ";" }
field_declarator         = { field_attribute_list? ~ type_spec ~ declarators }
field_attribute_list     = { "[" ~ field_attribute ~ ("," ~ field_attribute)* ~ "]" }

// Union declarator
union_type                 = { "union" ~ Identifier? ~ union_switch ~ "{" ~ union_body ~ "}" | "union" ~ Identifier? ~ "{" ~ union_body_n_e ~ "}" }
union_switch               = { "switch" ~ "(" ~ switch_type_spec ~ Identifier ~ ")" ~ union_name? }
switch_type_spec           = { primitive_integer_type | Identifier }
union_name                 = { Identifier }
union_body                 = { union_case+ }
union_body_n_e             = { union_case_n_e+ }
union_case                 = { union_case_label+ ~ union_arm | default_case }
union_case_n_e             = { union_case_label_n_e ~ union_arm | default_case_n_e }
union_case_label           = { "case" ~ const_exp ~ ":" }
union_case_label_n_e       = { "[" ~ "case" ~ "(" ~ const_exp ~ ("," ~ const_exp)* ~ ")" ~ "]" }
default_case               = { "default" ~ ":" ~ union_arm }
default_case_n_e           = { "[" ~ "default" ~ "]" ~ union_arm }
union_arm                  = { field_declarator? ~ ";" }
union_type_switch_attr     = { "switch_type" ~ "(" ~ switch_type_spec ~ ")" }
union_instance_switch_attr = { "switch_is" ~ "(" ~ attr_var ~ ")" }

// Enumeration type
enumeration_type = { "enum" ~ Identifier ~ "{" ~ Identifier ~ ("," ~ Identifier)* ~ "}" }

// Pipes
pipe_type = { "pipe" ~ Identifier }

// Arrays
array_bounds_declarator = { "[" ~ array_bound? ~ "]" | "[" ~ array_bounds_pair ~ "]" }
array_bounds_pair       = { array_bound ~ ".." ~ array_bound }
array_bound             = { "*" | Integer_literal | Identifier }

// Type attributes
type_attribute  = { "transmit_as" ~ "(" ~ xmit_type ~ ")" | "handle" | usage_attribute | union_type_switch_attr | ptr_attr }
usage_attribute = { "string" | "context_handle" }
range_attribute = { "range" ~ "(" ~ Integer_literal ~ "," ~ Integer_literal ~ ")" }
xmit_type       = { simple_type_spec }

// Field attributes
field_attribute = {
    "first_is" ~ "(" ~ attr_var_list ~ ")"
  | "last_is" ~ "(" ~ attr_var_list ~ ")"
  | "length_is" ~ "(" ~ attr_var_list ~ ")"
  | "min_is" ~ "(" ~ attr_var_list ~ ")"
  | "max_is" ~ "(" ~ attr_var_list ~ ")"
  | "size_is" ~ "(" ~ attr_var_list ~ ")"
  | range_attribute
  | usage_attribute
  | union_instance_switch_attr
  | "ignore"
  | ptr_attr
}
attr_var_list   = { attr_var ~ ("," ~ attr_var)* }
attr_var        = { "*"? ~ (Integer_literal | Identifier) }

// Operation declarator
op_declarator         = { op_attributes? ~ simple_type_spec ~ Identifier ~ param_declarators }
op_attributes         = { "[" ~ op_attribute ~ ("," ~ op_attribute)* ~ "]" }
op_attribute          = { "idempotent" | "broadcast" | "maybe" | "reflect_deletions" | usage_attribute | ptr_attr }
param_declarators     = { "(" ~ ("void" | (param_declarator ~ ("," ~ param_declarator)*)?) ~ ")" }
param_declarator      = { param_attributes? ~ type_spec ~ declarators }
param_attributes      = { "[" ~ param_attribute ~ ("," ~ param_attribute)* ~ "]" }
param_attribute       = { directional_attribute | field_attribute }
directional_attribute = { "in" | "out" | "inout" }

// Function pointers
function_declarator          = { direct_declarator_base ~ param_declarators }

// Pointer
declarator  = { pointer_opt ~ direct_declarator }
pointer_opt = { pointer? }
pointer     = { "*" }

// Types
base_type_spec         = { base_type ~ pointer_opt }
base_type              =       { floating_pt_type | primitive_integer_type | byte_type | void_type }
floating_pt_type       = { "float" | "double" }
primitive_integer_type = { signed_integer | unsigned_integer }
signed_integer         = { integer_size ~ ("int")? }
integer_size           = { "long long" | "long" | "short" | "char" }
unsigned_integer       = { (("unsigned" ~ integer_size) | integer_size ~ "unsigned") ~ ("int")? }
byte_type              = { "byte" }
void_type              = { "void" }

// character_constant - contains all chars from the portable charset, wrapped in single quotes,
// unless it's single quote, in which case it's escaped with a backslash
character_constant = @{ "'" ~ (!"'" ~ ANY)* ~ "'" }
// same goes for string here.
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

/* * TERMINALS * */
Integer_literal = @{ ASCII_DIGIT+ }
Uuid_rep        = @{ ASCII_HEX_DIGIT{8} ~ "-" ~ (ASCII_HEX_DIGIT{4} ~ "-"){3} ~ ASCII_HEX_DIGIT{12} }
Identifier      = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | ASCII_DIGIT | "_"){,31} }
Import_string   = @{ (ASCII_ALPHA | ASCII_DIGIT | "_" | "-" | "." | "/") }

/* * SPECIAL TOKENS * */
WHITESPACE = _{ " " | "\r" | "\n" | "\t" }
COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!"\n" ~ ANY)* ~ "\n" }
